// Establish version and status
ext.githubProjectName = "ScriptLib" // Change if github project name is not the same as the root project's name

buildscript {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    repositories { 
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
    }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {
    group = "com.netflix.${githubProjectName}" // TEMPLATE: Set to organization of project
    apply plugin: 'java'
    test {
        // enable TestNG support (default is JUnit)
        useTestNG()
    }
    dependencies {
        compile     'org.slf4j:slf4j-api:1.6.3'
        compile     'com.google.code.findbugs:jsr305:2.0.1'
        compile     'commons-lang:commons-lang:2.6'
        compile     'commons-io:commons-io:2.4'
        compile     'commons-collections:commons-collections:3.2.1'
        testCompile 'org.testng:testng:6.1.1'
        testCompile 'org.mockito:mockito-core:1.8.5'
    }
}

project(':scriptlib-core') {
    dependencies {
        compile 'org.jboss.modules:jboss-modules:1.3.0.Beta3'
        compile 'com.google.code.gson:gson:2.2.4'
    }
}

project(':scriptlib-groovy2') {
    dependencies {
        compile project(':scriptlib-core')
        compile 'org.codehaus.groovy:groovy-all:2.1.6'
    }
}