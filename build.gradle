buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:9.0.1'
        classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:8.6.0'
    }
}

plugins {
  id 'me.champeau.gradle.jmh' version '0.5.0'
  id "org.gretty" version "3.0.1"
}

ext.githubProjectName = "Nicobar"

allprojects {
  apply plugin: 'nebula.dependency-recommender'
  apply plugin: 'project-report'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'nebula.netflixoss'
}

subprojects {
    group = "com.netflix.${githubProjectName}"

    repositories {
        jcenter()
        mavenLocal()
    }

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    apply plugin: 'java-library'
    apply plugin: 'build-dashboard'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    licenseTest.enabled = false

    test {
        // enable TestNG support (default is JUnit)
        useTestNG()
    }

    dependencies {
        compile     'org.slf4j:slf4j-api:1.7.0'
        compile     'com.google.code.findbugs:jsr305:2.0.2'
        compile     'com.google.code.findbugs:annotations:2.0.2'
        compile     'commons-lang:commons-lang:2.6'
        compile     'commons-io:commons-io:2.4'
        compile     'commons-collections:commons-collections:3.2.2'
        compile     'net.sf.jgrapht:jgrapht:0.8.3'
        testCompile 'org.testng:testng:6.1.1'
        testCompile 'org.mockito:mockito-core:1.10.8'
        testCompile 'org.slf4j:slf4j-simple:1.7.0'
    }
}